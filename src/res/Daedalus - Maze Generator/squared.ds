DS
// Survivor Squared Off game - A script file for Daedalus 3.2
// By Walter D. Pullen, Astara@msn.com, http://www.astrolog.org/labyrnth.htm

// Variable usage:
// a-h: Colors for the eight players (constant)
// i-p: X and Y position of the eight players
// q: User player index
// r: Current player
// s: Players left in game
// t: Number of moves available
// u: Current direction
// v: Which move to take
// w: Temporary variable
// x-y: X and Y coordinates of player
// z: Event return value

If Gte Version 2500 {Break} Message 'This script requires Daedalus 2.5 or later to run.' Break

DefineMacro 1 {@w Inc @q Message 'Welcome to Survivor!\n\nThis simulates the Squared Off game from the episode 9 immunity\nchallenge in the original "Survivor" series. Your goal is to be the\nlast player standing, by running the other seven players out of\nroom. On your turns, move to an adjacent square that hasn't yet\nbeen visited, for as long as you're able.\n\nF1: Display this help text.\nF2: Start new game.\nF3: Let other players continue (if you've lost).\n\nIn this game you are player #@w ($w).\nJeff Probst says: "Survivors ready? Go!"'} "Help Text"
DefineMacro 2 "fRedrawWhenBitmapEdited False fShowColorBitmap True _ AllClear _ | k \ k Macro20 fRedrawWhenBitmapEdited True n Macro1 Macro8" "Restart Game"
DefineMacro 3 "If Lte @s 1 {Message 'The game has finished.' Break} If Neq Var Add 9 @q -1 {Message 'You are still in the game.' Break} @r And Inc @r 7 Macro8" "User Wait"

DefineMacro 4 "Message 'Jeff Probst says: "Hey, you can't step off the playing field!"' @z True" "Edge Event"
DefineMacro 5 "If Gte @x 10 {Macro4 Break} If GetE @x @y {Message 'You bump into another player!' @z True Break} @w GetCT @x @y If Neq @w Black {Message 'That square has already been visited!' @z True Break} fRedrawWhenBitmapEdited False SetC @x @y Var Inc @q SetCT @x @y White Macro18 @r And Inc @r 7 fRedrawWhenBitmapEdited True @z False" "Before Move"
DefineMacro 6 "fRedrawWhenBitmapEdited False nHorizontalOffset 0 nVerticalOffset 0 fRedrawWhenBitmapEdited True n Macro8" "After Move"
DefineMacro 7 "@x Inc @q IfElse Neq @r @q {@y Inc @r MessageInside 'You: Player #@x ($x). Current turn: Player #@y ($y)' -1} {MessageInside 'You: Player #@x ($x). It's your turn!' -1}" "Inside Event"

DefineMacro 8 "If Lte @s 1 {Break} If Equ @r @q {Macro10 Spc Break} Macro9 @r And Inc @r 7 Restart" "Move All"
DefineMacro 9 "If Equ Var Add 9 @r -1 {Break} fRedrawWhenBitmapEdited False Macro17 SetE @x @y False Macro14 If Lte @t 0 {Macro11 fRedrawWhenBitmapEdited True n Break} @v Rnd 1 @t For (u) 0 7 {Macro17 Macro16 Macro15 If @z '@v Dec @v' If Lte @v 0 'Break'} SetC @x @y Var Inc @r SetCT @x @y White SetE @x @y True Macro18 fRedrawWhenBitmapEdited True Delay nRepeatDelayInMsec n" "Move Computer"
DefineMacro 10 (If Equ Var Add 9 @q -1 {Break} Macro14 If Gt @t 0 {Break} @w Dec @s Message 'You can't move and are out of the game! :-(\nNumber of players finishing better: @w\nYou get sent over to the "loser bench" to watch the rest of the players still in this game.\n\nF1: Display help text.\nF2: Start new game.\nF3: Let the other players still in the game take a turn.' fRedrawWhenBitmapEdited False Macro11 kSky Black kCloud DkGray kCloud2 Dark kCloud fStars True Macro13 fRedrawWhenBitmapEdited True n) "Check User"
DefineMacro 11 "@x 11 @y Add 1 @r SetC @x @y Var Inc @r Macro19 @s Dec @s IfElse Neq @r @q {@w Inc @r Message 'Player #@w ($w) can't move and is out of the game!\nNumber of players left: @s' SetE 11 Add 1 @r True} {nX 11 nY Add 1 @r nDir 1 nDirectionOffset 0} SetVar Add 9 @r -1 Macro12" "Die"
DefineMacro 12 "If Gt @s 1 {Break} IfElse Equ Var Add 9 @q -1 {Message 'One player left, we have a winner!\n\nUnfortunately the winner wasn't you. You overhear the other\nplayers plotting to vote you off the island this Tribal Council. :-('} {fRainbow True Message 'You win! You are the sole surviving player left standing! :-)\n\nJeff congratulates you, and hands you the coveted immunity\nnecklace. You try to keep from laughing as the other tribemates\nfrantically try to form an alliance with you.'} Macro13" "Check Winner"

DefineMacro 13 "ClearEvents nMotionVelocity 50" "Disengage User"
DefineMacro 14 "@t 0 For (u) 0 7 {Macro17 Macro16 Macro15 @t Add @t @z}" "Count Square"
DefineMacro 15 "If Or Or Or Lt @x 0 Lt @y 0 Gte @x 10 Gte @y 10 {@z 0 Break} @w GetC @x @y If And Neq @w LtGray Neq @w Light LtGray {@z 0 Break} @z 1" "Check Square"
DefineMacro 16 "If And Gte @u 1 Lte @u 3 {@x Dec @x} If Gte @u 5 {@x Inc @x} If Or Lte @u 1 Gte @u 7 {@y Dec @y} If And Gte @u 3 Lte @u 5 {@y Inc @y}" "Get Dir"
DefineMacro 17 "@x Div Var Add 9 @r 10 @y Mod Var Add 9 @r 10" "Get XY"
DefineMacro 18 "SetVar Add 9 @r Add Mul @x 10 @y Macro19" "Set XY"
DefineMacro 19 "SetX @x @y -1 Inc @r" "Set Texture"
DefineMacro 20 "nOnMoveOffBitmap 4 nOnMoveToNewCell 5 nAfterMoveToNewCell 6 nAfterRedrawInside 7 kSky Cyan kCloud White kCloud2 Light Light LtGray fStars False nMotionVelocity 140 nHorizontalOffset 0 nVerticalOffset 0 nDirectionOffset 0 fRainbow False SetVars 9 8 '20 02 70 92 07 29 97 79' For (r) 0 7 {@w Rnd 0 7 SwapVar Add 9 @r Add 9 @w} @q Rnd 0 7 kDot Light Var Inc @q For (r) 0 7 {Macro17 SetC @x @y Var Inc @r SetCT @x @y White IfElse Neq @r @q (SetE @x @y True) (nX @x nY @y) Macro19} nDir Add << Or Lte nY 0 Lte nX 0 1 Or Lte nX 0 Gte nX 9 g @r 0 @s 8" "Reset Variables"

fRedrawWhenBitmapEdited False ResetProgram WindowTitle "- Survivor Squares" True nHorizontalScroll 8 nVerticalScroll 8 fHideMenu True fHideScrollbars True nTextureWall 0
kGround Light Brown kMountain2 Brown kMountain kGround f3D False fShowDot True fDotIsCircle True nDotSize 0 fNoCornerHopping False fMoveByTwo False nEdgeBehavior 0 nInside 3 fNarrowWalls False fCellLocation True fSealedEntrances False fMountains True nPeakHeight 20 nRotationVelocity 15 nSemitransparentWalls 3 nCellSize 100 nWallHeight 100 rViewingSpan 6000
SetVars 1 8 "Red Orange Yellow Green Cyan Blue Purple Magenta" SetStrings 0 17 ",T_0_,Red,Orange,Yellow,Green,Cyan,Blue,Purple,Magenta"
Size 72 96 0 1 Turtle "T1" For (r) 1 8 {Del SetChar 0 2 Add 48 @r Turtle "BM11,11S12" Turtle $0 Thicken2 8 cDel CopyMask -1 @r}
Size 10 10 0 1 Quo = k AllSet Size 12 10 0 0 Turtle "BM11,1S1D7" = Quo Replace White LtGray 0 k AllClear Blend ColorOr Replace White Light LtGray 0 \ AllClear CopyTexture -1 0 | k AllClear \ k
Macro20 fRedrawWhenBitmapEdited True n Macro1 Macro8
